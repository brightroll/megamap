#!/usr/bin/perl

use strict;
use warnings;
use Data::Dumper;
use Readonly;

our $VERSION = 0.8;
Readonly my $END_WINDOW => 2;

my $username = ( getpwuid $> );
die "megacli requires root" if $username ne 'root';

my $megacli = `megacli -pdlist -a0 | egrep 'Slot|^SAS'`;
my @megalines = split /\n/, $megacli;

if ( scalar(@megalines) == 0 ) {
	warn "No output from megacli\n";
	exit 2;
}

my $slot;
my %slot_map;
foreach my $line (@megalines) {
	if ( $line =~ /^Slot Number/ ) {
		$line =~ s/.* //;
		$slot = $line;
	}
	if ( $line =~ /^SAS Address\(0\)/ ) {
		$line =~ s/.* //;
		$slot_map{$slot} = { megacli_sas => $line };
		my $linux = $line;
		my $end = hex substr( $linux, 0 - $END_WINDOW );
		$end--;
		$end = sprintf "%02x", $end;
		substr( $linux, 0 - $END_WINDOW ) = $end;
		$slot_map{$slot}->{linux} = $linux;
		my $ls_out = `ls -l /dev/disk/by-id | grep $linux | grep -v part`;
		chomp($ls_out);

		if ( !length $ls_out ) {
			$slot_map{$slot}->{dev} = q{?};
			next;
		}
		$ls_out =~ s/.* //;
		$ls_out =~ s{[./]}{}g;
		$slot_map{$slot}->{dev} = $ls_out;
	}
}

#print Dumper(\%slot_map);

foreach my $slot ( sort { $a <=> $b } keys %slot_map ) {
	my $dev   = $slot_map{$slot}->{dev};
	my $linux = $slot_map{$slot}->{linux};
	print "$slot\t$dev\t$linux\n";
}

